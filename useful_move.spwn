
extract obj_props

impl @group {
    
    moveAtX: #[desc("Implementation of the move trigger at X")] 
    (
        self, 
        #[desc("At what X to move")] at: @number, 
        #[desc("Units to move on the X axis")] x: @number, 
        #[desc("Units to move on the Y axis")] y: @number, 
        #[desc("Duration of movement")] duration: @number = 0, 
        easing: @easing_type = NONE, 
        easing_rate: @number = 2
    ) {
        $.add( obj{
            OBJ_ID: 901,
            TARGET: self,
            X: at,
            Y: 300,
            MOVE_X: x * 3,
            MOVE_Y: y * 3,
            DURATION: duration,
            EASING: easing.id,
            EASING_RATE: easing_rate,
        })
    },
    moveAtXY: #[desc("Implementation of the move trigger at X and Y")] 
    (
        self, 
        #[desc("At what X to move")] atX: @number, 
        #[desc("At what Y to move")] atY: @number, 
        #[desc("Units to move on the X axis")] x: @number, 
        #[desc("Units to move on the Y axis")] y: @number, 
        #[desc("Duration of movement")] duration: @number = 0, 
        easing: @easing_type = NONE, 
        easing_rate: @number = 2
    ) {
        $.add( obj{
            OBJ_ID: 901,
            TARGET: self,
            X: atX,
            Y: atY,
            MOVE_X: x * 3,
            MOVE_Y: y * 3,
            DURATION: duration,
            EASING: easing.id,
            EASING_RATE: easing_rate,
            TOUCH_TRIGGERED: true,
        })
    },
    moveAtY: #[desc("Implementation of the move trigger at Y")] 
    (
        self, 
        #[desc("At what Y to move")] at: @number, 
        #[desc("Units to move on the X axis")] x: @number, 
        #[desc("Units to move on the Y axis")] y: @number, 
        #[desc("Duration of movement")] duration: @number = 0,  
        easing: @easing_type = NONE, 
        easing_rate: @number = 2
    ) {
        
        // Collision blocks
        id = ?b
        grp = ?g
        $.add( obj{
            OBJ_ID: 1816,
            Y: at,
            GROUPS: grp,
            DYNAMIC_BLOCK: true,
            BLOCK_A: id,
        })
        id1 = ?b
        grp1 = ?g
        $.add( obj{
            OBJ_ID: 1816,
            Y: 15,
            GROUPS: grp1,
            BLOCK_A: id1,
        })
        
        grp.lock_to_player(lock_x = true, lock_y = false, duration = 99999)
        grp.alpha(0)
        grp1.lock_to_player(lock_x = true, lock_y = true, duration = 99999)
        grp1.alpha(0)
        
        on(collision(id, id1), !{self.move(x, y, duration, easing, easing_rate)})
    },
}